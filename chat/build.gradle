//plugins {
//    id 'com.android.application'
//}

if(isRelease){
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}



android {
    namespace 'com.example.chat'
    compileSdk app_android.compileSdkVersion

    defaultConfig {
        if(!isRelease){
            applicationId "com.example.chat"
            versionCode 1
            versionName "1.0"
        }

        minSdk app_android.minSdkVersion
        targetSdk app_android.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (!isRelease) {
                //如果是组件模式，单独运行模式
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                //集成化模式，整个项目打包
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //release时 debug目录下文件不需要合并到主工程
                    exclude '**/debug/**'
                }
            }
        }
    }

}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(':common')
}